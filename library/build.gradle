//文件开头配置
apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion 26

    resourcePrefix "ts_"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 26
        versionCode 15
        versionName "1.5"
    }
}

publish {
    userOrg = 'getactivity'//填写bintray用户名，注意大小写
    groupId = 'com.hjq'//定义的maven group id最终引用形式
    artifactId = 'toast'//maven的artifact id
    version = '1.5'//maven 上发布版本号
    description = 'A simple generic title bar'//描述，自己定义
    website = "https://github.com/getActivity/ToastUtils"//项目在github中的地址
}

tasks.withType(Javadoc) {//防止编码问题
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}